; Free Look - Stentorious

; On game load
; Inject nodes
if eval !ModelHasBlock Player "FreelookPitch"
	InsertNode Player 3 "Camera1st|^FreelookPitch"
endif
if eval !ModelHasBlock Player "FreelookYaw"
	InsertNode Player 3 "FreelookPitch|^FreelookYaw"
endif
if eval !ModelHasBlock Player "FreelookOffset"
	InsertNode Player 3 "FreelookYaw|^FreelookOffset"
endif

; Reset node rotations
Player.SetNifRot "FreelookPitch" 0 0 0 0 2
Player.SetNifRot "FreelookYaw" 0 0 0 0 2
Player.SetNifRot "FreelookOffset" 0 0 0 0 2
Goo1.AuxVarSetFlt "*Freelook_Cache" 0 0	; Pitch
Goo1.AuxVarSetFlt "*Freelook_Cache" 0 1	; Yaw
Goo1.AuxVarSetFlt "*Freelook_Cache" 0 2	; Offset
Goo1.AuxVarSetFlt "*Freelook_Cache" 0 3	; Pitch offset limit
Goo1.AuxVarSetFlt "*Freelook_Cache" 0 4	; fAutoAimMaxDegrees

; Reset free look
if eval Goo1.AuxVarGetFlt "*Freelook"
	SetNumericGameSetting "fAutoAimMaxDegrees" (Goo1.AuxVarGetFlt "*Freelook_Cache" 4)
	ToggleMouseMovement 3
	SetStickDisabled 1 0
	EC 5
	EC 6
	SetUIFloatAlt "HUDMainMenu\JDC\_visible_stent" 1
	SetUIFloatAlt "HUDMainMenu\ReticleCenter\reticle_center\alpha" 255
	SetUIFloatAlt "HUDMainMenu\FreelookDot\_visible" 0
	Goo1.AuxVarSetFlt "*Freelook" 0
endif

; On game restart
if eval Goo1.AuxVarGetFlt "*Freelook_Init"
	return
endif
Goo1.AuxVarSetFlt "*Freelook_Init" 1

; Requirements check
if eval GetNVSEVersion < 6 || (GetNVSEVersion == 6 && GetNVSERevision < 4)
	MessageBoxEx "Tactical Freelook missing requirement!%rInstall xNVSE 6.4.0+."
	return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 250
	MessageBoxEx "Tactical Freelook missing requirement!%rInstall latest JohnnyGuitar NVSE."
	return
endif
if eval GetPluginVersion "ShowOffNVSE Plugin" < 145
	MessageBoxEx "Tactical Freelook missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif

; Load INI settings
Goo1.AuxVarSetFlt "*Freelook_INI" (GetMaxOf 0 (GetINIFloat_Cached "General:bAllowAttacking" "Stentorious\Freelook.ini" 0)) 0
Goo1.AuxVarSetFlt "*Freelook_INI" (GetMaxOf 0 (GetINIFloat_Cached "General:bAllowAiming" "Stentorious\Freelook.ini" 0)) 1
Goo1.AuxVarSetFlt "*Freelook_INI" (Clamp (GetINIFloat_Cached "General:iReticleBehavior" "Stentorious\Freelook.ini" 1) 0 2) 2
Goo1.AuxVarSetFlt "*Freelook_INI" (Clamp (GetINIFloat_Cached "Camera:fLimitYaw" "Stentorious\Freelook.ini" 80) 10 170) 3
Goo1.AuxVarSetFlt "*Freelook_INI" (Clamp (GetINIFloat_Cached "Camera:fLimitPitchOffset" "Stentorious\Freelook.ini" 20) 0 80) 4
Goo1.AuxVarSetFlt "*Freelook_INI" ((GetMaxOf 0.1 (GetINIFloat_Cached "Camera:fSensitivityMouse" "Stentorious\Freelook.ini" 1)) * 40) 5
Goo1.AuxVarSetFlt "*Freelook_INI" (GetMaxOf 0.1 (GetINIFloat_Cached "Camera:fSensitivityController" "Stentorious\Freelook.ini" 1)) 6
Goo1.AuxVarSetFlt "*Freelook_INI" ((GetMaxOf 0.1 (GetINIFloat_Cached "Camera:fRecenterMouse" "Stentorious\Freelook.ini" 1)) * -10) 7
Goo1.AuxVarSetFlt "*Freelook_INI" ((GetMaxOf 0.1 (GetINIFloat_Cached "Camera:fRecenterController" "Stentorious\Freelook.ini" 0.5)) * -5) 8

if eval (int iKeyID = GetINIFloat_Cached "General:iKeyBind" "Stentorious\Freelook.ini" 56) > 0
	SetOnKeyDownEventHandler (CompileScript "Freelook\OnKeyDown.gek") 1 iKeyID
	SetOnKeyUpEventHandler (CompileScript "Freelook\OnKeyUp.gek") 1 iKeyID
endif

; Intercept TogglePOV controls
if eval GetINIFloat_Cached "General:bUseTogglePOV" "Stentorious\Freelook.ini"
	SetOnControlDownEventHandler (CompileScript "Freelook\OnControlDown.gek") 1 13
	SetOnControlUpEventHandler (CompileScript "Freelook\OnControlUp.gek") 1 13
endif

; Sensitivity update
Goo1.AuxVarSetFlt "*_Controls:fMouseSensitivity" (GetNumericIniSetting "fMouseSensitivity:Controls" / 0.00125)
SetJohnnyOnSettingsUpdateEventHandler 1 ({} => Goo1.AuxVarSetFlt "*_Controls:fMouseSensitivity" (GetNumericIniSetting "fMouseSensitivity:Controls" / 0.00125))

CloseFileSO "Stentorious\Freelook.ini" 0
