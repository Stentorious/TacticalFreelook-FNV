float fCamX
float fCamY
float fCamZ
float fLimitYaw
float fDeltaTime
float fSmoothRate
float fYaw
float fPitch
float fOffset
float fSensitivity
float fInputMag

begin Function {}

	if eval (fDeltaTime = GGetSecPass) <= 0
		return
	endif

	; Smoothly recenter camera on free cam finish
	if eval Goo1.AuxVarGetFlt "*Freelook" == 0

		fPitch = Goo1.AuxVarGetFlt "*Freelook_Cache" 0
		fYaw = Goo1.AuxVarGetFlt "*Freelook_Cache" 1
		fOffset = (Goo1.AuxVarGetFlt "*Freelook_Cache" 2) * -1
		if eval ABS (fPitch - fOffset) >= 0.025 || ABS fYaw >= 0.025
			if eval GetController
				fSmoothRate = 1 - Exp (fDeltaTime * (Goo1.AuxVarGetFlt "*Freelook_INI" 8))
			else
				fSmoothRate = 1 - Exp (fDeltaTime * (Goo1.AuxVarGetFlt "*Freelook_INI" 7))
			endif
			Goo1.AuxVarSetFlt "*Freelook_Cache" (fPitch = Lerp (Goo1.AuxVarGetFlt "*Freelook_Cache" 0) fOffset fSmoothRate) 0
			Goo1.AuxVarSetFlt "*Freelook_Cache" (fYaw = Lerp (Goo1.AuxVarGetFlt "*Freelook_Cache" 1) 0 fSmoothRate) 1
			Player.SetNifRot "FreelookPitch" fPitch 0 0 0 2
			Player.SetNifRot "FreelookYaw" 0 0 fYaw 0 2
		elseif eval ABS (fPitch - fOffset) < 0.025 && ABS fYaw < 0.025
			Player.SetNifRot "FreelookPitch" 0 0 0 0 2
			Player.SetNifRot "FreelookYaw" 0 0 0 0 2
			Player.SetNifRot "FreelookOffset" 0 0 0 0 2
			Goo1.AuxVarSetFlt "*Freelook_Cache" 0 0
			Goo1.AuxVarSetFlt "*Freelook_Cache" 0 1
			Goo1.AuxVarSetFlt "*Freelook_Cache" 0 2
			Goo1.AuxVarSetFlt "*Freelook_Cache" 0 3
			SGMLC (CompileScript "Freelook\MainLoop.gek") 0
		endif
		return
	endif

	; Reset camera
	if eval MenuMode || Player.GetIsRagdolled || Player.GetDead || IsInKillCam || GetPlayerControlsDisabled 1 0 0 0 1 0 0
		Goo1.AuxVarSetFlt "*Freelook" 0
		Player.SetNifRot "FreelookPitch" 0 0 0 0 2
		Player.SetNifRot "FreelookYaw" 0 0 0 0 2
		Player.SetNifRot "FreelookOffset" 0 0 0 0 2
		Goo1.AuxVarSetFlt "*Freelook_Cache" 0 0
		Goo1.AuxVarSetFlt "*Freelook_Cache" 0 1
		Goo1.AuxVarSetFlt "*Freelook_Cache" 0 2
		Goo1.AuxVarSetFlt "*Freelook_Cache" 0 3
		SetNumericGameSetting "fAutoAimMaxDegrees" (Goo1.AuxVarGetFlt "*Freelook_Cache" 4)
		ToggleMouseMovement 3
		SetStickDisabled 1 0
		EnablePlayerControlsAltEx 30132
		EC 5
		EC 6
		SetUIFloatAlt "HUDMainMenu\JDC\_visible_stent" 1
		SetUIFloatAlt "HUDMainMenu\ReticleCenter\reticle_center\alpha" 255
		SetUIFloatAlt "HUDMainMenu\FreelookDot\_visible" 0
		SGMLC (CompileScript "Freelook\MainLoop.gek") 0
		return
	endif

	; Controller
	if eval GetController
		fCamX = GetRSX
		fCamY = GetRSY * -1
		if eval (fInputMag = V3Length fCamX fCamY 0) > 1
			fCamX /= fInputMag
			fCamY /= fInputMag
		endif
		if eval fInputMag == 0
			return
		endif
		fSensitivity = (Goo1.AuxVarGetFlt "*Freelook_INI" 6) * (Goo1.AuxVarGetFlt "*_Controls:fMouseSensitivity")

	; Mouse
	else
		GetCameraMovement (fCamX) (fCamY)
		if eval fCamX == 0 && fCamY == 0
			return
		endif
		fSensitivity = Goo1.AuxVarGetFlt "*Freelook_INI" 5
	endif

	fLimitYaw = Goo1.AuxVarGetFlt "*Freelook_INI" 3
	fPitch = Clamp ((Goo1.AuxVarGetFlt "*Freelook_Cache" 0) + (fCamY * fSensitivity)) -89 (Goo1.AuxVarGetFlt "*Freelook_Cache" 3)	; Get pitch angle
	fYaw = Clamp ((Goo1.AuxVarGetFlt "*Freelook_Cache" 1) + (fCamX * fSensitivity)) (-(fLimitYaw)) fLimitYaw						; Get yaw angle

	; Cache
	Goo1.AuxVarSetFlt "*Freelook_Cache" fPitch 0
	Goo1.AuxVarSetFlt "*Freelook_Cache" fYaw 1

	; Update node rotation
	Player.SetNifRot "FreelookYaw" 0 0 fYaw 0 2
	Player.SetNifRot "FreelookPitch" fPitch 0 0 0 2

end
