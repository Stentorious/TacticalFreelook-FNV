float fCamX
float fCamY
float fCamZ
float fLimitYaw
float fDeltaTime
float fSmoothRate
float fYaw
float fPitch
float fOffset
float fSensitivity

begin Function {}

	if eval (fDeltaTime = GGetSecPass) <= 0
		return
	endif

	if eval Goo1.AuxVarGetFlt "*Freelook"

		if eval MenuMode || Player.GetIsRagdolled || Player.GetDead || IsInKillCam || GetPlayerControlsDisabled 1 0 0 0 1 0 0
			Goo1.AuxVarSetFlt "*Freelook" 0
			Player.SetNifRot "FreelookPitch" 0 0 0 0 2
			Player.SetNifRot "FreelookYaw" 0 0 0 0 2
			Player.SetNifRot "FreelookOffset" 0 0 0 0 2
			Goo1.AuxVarSetFlt "*Freelook_Cache" 0 0
			Goo1.AuxVarSetFlt "*Freelook_Cache" 0 1
			Goo1.AuxVarSetFlt "*Freelook_Cache" 0 2
			Goo1.AuxVarSetFlt "*Freelook_Cache" 0 3
			SetNumericGameSetting "fAutoAimMaxDegrees" (Goo1.AuxVarGetFlt "*Freelook_Cache" 4)
			ToggleMouseMovement 3
			;SetStickDisabled 1 0
			EnablePlayerControlsAltEx 30132
			EC 5
			EC 6
			SetUIFloatAlt "HUDMainMenu\JDC\_visible_stent" 1
			SetUIFloatAlt "HUDMainMenu\ReticleCenter\reticle_center\alpha" 255
			SetUIFloatAlt "HUDMainMenu\FreelookDot\_visible" 0
			SGMLC (CompileScript "Freelook\MainLoop.gek") 0
			return
		endif

		GetCameraMovement (fCamX) (fCamY)

		; Get pitch angle
		fSensitivity = Goo1.AuxVarGetFlt "*Freelook_INI" 5
		fPitch = Clamp ((Goo1.AuxVarGetFlt "*Freelook_Cache" 0) + (fCamY * fSensitivity)) -89 (Goo1.AuxVarGetFlt "*Freelook_Cache" 3)
		Goo1.AuxVarSetFlt "*Freelook_Cache" fPitch 0

		; Get yaw angle
		fLimitYaw = Goo1.AuxVarGetFlt "*Freelook_INI" 3
		fYaw = Clamp ((Goo1.AuxVarGetFlt "*Freelook_Cache" 1) + (fCamX * fSensitivity)) (-(fLimitYaw)) fLimitYaw
		Goo1.AuxVarSetFlt "*Freelook_Cache" fYaw 1

		;if eval fPitch > 0
		;	fLimitYaw = 80 * sqrt (1 - (fPitch / 30)^2)
		;else
		;	fLimitYaw = 80
		;endif

		; Update node rotation
		Player.SetNifRot "FreelookYaw" 0 0 fYaw 0 2
		Player.SetNifRot "FreelookPitch" fPitch 0 0 0 2

		return

	; Recenter camera on free cam finish
	else
		Player.GetNifRotAlt "FreelookPitch" (fPitch) (fCamY) (fCamZ) 0 2
		Player.GetNifRotAlt "FreelookYaw" (fCamX) (fCamY) (fYaw) 0 2
		fOffset = (Goo1.AuxVarGetFlt "*Freelook_Cache" 2) * -1
		if eval ABS (fPitch - fOffset) >= 0.1 || ABS fYaw >= 0.1
			fSmoothRate = 1 - Exp (fDeltaTime * (Goo1.AuxVarGetFlt "*Freelook_INI" 6))
			Goo1.AuxVarSetFlt "*Freelook_Cache" (fPitch = Lerp (Goo1.AuxVarGetFlt "*Freelook_Cache" 0) fOffset fSmoothRate) 0
			Goo1.AuxVarSetFlt "*Freelook_Cache" (fYaw = Lerp (Goo1.AuxVarGetFlt "*Freelook_Cache" 1) 0 fSmoothRate) 1
			Player.SetNifRot "FreelookPitch" fPitch 0 0 0 2
			Player.SetNifRot "FreelookYaw" 0 0 fYaw 0 2
		elseif eval ABS (fPitch - fOffset) < 0.1 && ABS fYaw < 0.1
			Player.SetNifRot "FreelookPitch" 0 0 0 0 2
			Player.SetNifRot "FreelookYaw" 0 0 0 0 2
			Player.SetNifRot "FreelookOffset" 0 0 0 0 2
			Goo1.AuxVarSetFlt "*Freelook_Cache" 0 0
			Goo1.AuxVarSetFlt "*Freelook_Cache" 0 1
			Goo1.AuxVarSetFlt "*Freelook_Cache" 0 2
			Goo1.AuxVarSetFlt "*Freelook_Cache" 0 3
			SGMLC (CompileScript "Freelook\MainLoop.gek") 0
		endif
	endif

end
